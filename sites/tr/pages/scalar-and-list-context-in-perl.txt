=title Perl programlarında tekli ve çoklu bağlam, bir dizideki eleman sayısı
=timestamp 2014-08-31T14:14:14
=indexes scalar, list, array, size, length, context, Perl
=status show
=original scalar-and-list-context-in-perl
=books beginner
=author szabgab
=translator nanis
=comments_disqus_enable 0

=abstract start

<a href="/perl-tutorial">Perl dersimizin</a> bu bölümünde Perl'de <b>bağlam duyarlılığını</b> işleyeceğiz.

Her dilde olduğu gibi Türkçe'de de aynı kelime farklı bağlamlarda farklı anlamlar ifade edebilir. Mesela "çakmak" fiili birçok anlam içerir:

Çivi çakmak

Çakmak çakmak

Kelimenin çevresindeki metin, yani kelimenin bulunduğu bağlam, kelimeyi doğru anlamamızı sağlar.

Perl 5 de buna benzer. Kelimeler, fonksiyon çağrıları ve diğer ifadeler bulundukları bağlamlara göre farklı anlamlar ifade edebilir. Bu durum öğrenmeyi zorlaştırır, ama ifade kabiliyetimizi zenginleştirir.

=abstract end

Perl dilinde <dfn title="scalar context">tekli bağlam</dfn> ve <dfn title="list context">çoklu bağlam</dfn> olmak üzere iki ana bağlam türü vardır.

<h2>Çoklu bağlamda dizi</h2>

Bir örnek verelim:

<code lang="perl">
my @words = ('Foo', 'Bar', 'Baz');
my @names = @words;
</code>

Yukaridaki atama sonrası <hl>@names</hl> dizisi <hl>@words</hl> dizi değerlerinin kopyalarını içerir.

Bir diziyi başka bir diziye atama işlemi dizinin içeriğini diğer diziye kopyalar.

<h2>Tekli bağlamda dizi</h2>

<code lang="perl">
my @words = ('Foo', 'Bar', 'Baz');
my $people =  @words;
</code>

Bu kez <hl>@words</hl> dizisini <hl>$people</hl> adlı <abbr title="scalar">tek değerli</abbr> bir değişkene atadık.

Başka dillerde bu farklı sonuçlar doğurur ama Perl dilinde bu atama işlemi <b>dizinin eleman sayısını</b> tek değerli değişkene kopyalar.

Hem keyfî hem de yukarıdaki kullanımın faydalı olduğu da söylenemez olsa da bu işlemin çok faydalı olduğu birçok başka durum vardır.

<h2>Tekli ve Çoklu Bağlam</h2>

Yukarıda tekli, yani tek bir değer beklediğimiz, ve çoklu, yani birden çok değer beklediğimiz, bağlam örnekleri gördük. Çoklu bağlamda bir ifade 0, 1, 2 ya da başka herhangi bir sayıda değer içerebilir.

<h2>if ifadesinin bağlamı</h2>

Aşağıdaki örneğe bakalım:

<code lang="perl">
my @words = ('Foo', 'Bar', 'Baz');

if (@words) {
   say "There are some words in the array";
}
</code>

<hl>if</hl> ifadesinin koşul kısmında tek bir değer bekliyoruz. Bu durumda beklenen tekli bağlam olmalı.

Böylece bir dizinin tekli bağlamdaki değerinin dizideki eleman sayısı olduğunu biliyoruz. Ayrıca boş bir dizideki eleman sayısın sıfır (yani <a href="/boolean-values-in-perl">yanlış</a>) olduğunu, bir veya daha fazla eleman içeriyor ise pozitif bir sayı (yani <a href="/boolean-values-in-perl">doğru</a>) olduğunu da biliyoruz.

Yani, yukarıda bahsettiğimiz keyfî işlem sayesinde, <hl>if (@words)</hl> kod ifadesi dizide herhangi bir eleman olup olmadığını sınar. Dizi boş ise koşul sağlanmamış olur.

if-koşulunu <hl>if (! @words)</hl> şekline çevirmemiz durumunda dizi boş ise sınama doğru değeri verir.

<h2>Tekli and Çoklu Bağlam</h2>

<a href="/the-year-19100">Bir önceki bölümde</a> <hl>localtime()</hl> ifadesinin tekli ve çoklu bağlamlarda nasıl davrandığını, şimdi ise bir dizinin tekli and çoklu bağlamlarda nasıl davrandığını gördük.

Bağlam üzerinde genel-geçer bir kural yoktur. Özel durumları öğrenmek zorundasınız ama genellikle bunlar çok açıktır. Ne olursa olsun, <a href="/core-perl-documentation-cpan-module-documentation">perldoc</a> kullanarak fonksiyonun dokümantasyonuna bakarsanız, tekli ve çoklu bağlamlara göre farklı değerler veren her fonksiyon için kısa bir açıklama göreceksiniz.

Şimdi biraz daha Perl ifadelerine ve nasıl bağlamlar oluşturduklarına bakalım.

<h2>Tekli bağlam oluşturmak</h2>

Tek değerli herhangi bir değişkene ne atamasi yaparsanız yapın bu işlemin
tekli bağlamda gerçekleştiğini gördük.  Bunu şöyle açıklayabiliriz:

<code>
$x = TEKDEĞER;
</code>

Dizinin her bir elemanı da tek bir değer tuttuğundan, bunlara yapılan atamalar da tekli bağlam oluşturur:

<code>
$word[3] = TEKDEĞER;
</code>

Birleştirme operatörü her iki tarafında karakter dizesi beklediği için aşağıdaki iki durumda da tekli bağlam oluşturur:

<code>
"string" . TEKDEĞER;
</code>

ve ayrıca

<code>
TEKDEĞER . "string"
</code>

Yani

<code lang="perl">
my @words = ('Foo', 'Bar', 'Baz');
say "Eleman sayisi: " . @words;
say "Simdi " . localtime();
</code>

programını çalıştırdığımızda

<code>
Eleman sayisi: 3
Simdi Thu Feb 30 14:15:53 1998
</code>

çıktısını verir.

Sayısal işlemler genellikle işlem imgesini her iki yanında iki sayı &mdash;iki tek değerli ifade&mdash; bekler. Bu nedenle sayısal işlemler her iki yanlarında tekli bağlam oluştururlar.

<code>
5 + TEKDEĞER;

TEKDEĞER + 5;
</code>

<h2>Çoklu bağlam oluşturma</h2>

Belli başlı bazı yapılar çoklu bağlam oluşturur:

Diziye atama yapmak bunlardan biridir:

<code>
@x = LİSTE;
</code>

Bir listeye atama yapmak diğer bir yoldur:

<code>
($x, $y) = LİSTE;
</code>

Bu liste tek bir eleman içerse dahi durum aynıdır:

<code>
($x) =  LİSTE;
</code>

Bu da bize başkalarını kolayca tuzağa düşürmek bir yöntem verir:

<h2>Parantezler ne zaman önemlidir?</h2>

<code lang="perl">
use strict;
use warnings;
use 5.010;

my @words = ('Foo', 'Bar', 'Baz');

my ($x) = @words;
my $y   = @words;

say $x;
say $y;
</code>

programı

<code>
Foo
3
</code>

çıktısını verir.

Bu, parantezlerin önemli olduğu nadir durumlardan biridir.

İlk atama olan <hl>my ($x) = @words;</hl> ifadesinde bir tekdeğerli değişken listesine atama yaptık. Bu işlem sağ tarafta çoklu bağlam oluşturdu. Yani dizinin <b>değerleri</b> sol taraftaki listeye kopyalandı. Sadece bir tek değerli değişken olduğundan, dizinin ilk elemanı kopyalandı, diğerleri kopyalanmadı.

İkinci atama olan <hl>my $y = @words;</hl> ifadesinde <b>doğrudan</b> tek değerli bir değişkene atama yaptık. Bu işlem sağ tarafta tekli bağlam oluşturdu.  Tekli bağlamda bir dizi, dizideki eleman sayısını verir.

Bu gözlem, <a href="/subroutines-and-functions-in-perl">fonksiyona parametre göndermek</a> konusuna geçtiğinizde çok önemli olacak.

<h2>Tekli bağlama zorlamak</h2>

Hem <hl>print()</hl> hem de <hl>say()</hl> parametreleri için çoklu bağlam oluştururlar. Ya bir dizideki eleman sayısını yazdırmak isterseniz ne yapacaksınız? Ya da <hl>localtime()</hl> fonksiyonunun verdiği formatlanmış tarihi yazdırmak isterseniz?

Şu programı bir deneyelim:

<code lang="perl">
use strict;
use warnings;
use 5.010;

my @words = ('Foo', 'Bar', 'Baz');

say @words;
say localtime();
</code>

Çıktısı:

<code>
FooBarBaz
3542071011113100
</code>

İlkinin anlaşılması bir şekilde mümkün, bunlar dizideki değerlerin peşisıra
yazılmış halidir.

İkinci ise çok daha kafa karıştırıcı. Bu sonuc <hl>time()</hl>
fonksiyonundan verdiği değer ile aynı değildir. Bu aslında
<hl>localtime()</hl> fonksiyonunun çoklu bağlamda verdiği dokuz sayının peşi
sıra yazılmış şeklidir. Hatırlamıyorsanız <a href="/the-year-19100">19100
yılı</a> adlı bölüme bir göz atın.

Çözüm, argümanına tekli bağlam yaratan <hl>scalar()</hl> fonksiyonunu
kullanmaktır.  Aslında <hl>scalar()</hl> fonksiyonunun tek görevi budur. Bu
fonksiyonu çoğul bir ifadeyi tekile dönüştüren <b>casting</b> gibi düşünmek
mümkün ama Perl dünyasında bu kelimenin pek kullanılmadığını sanıyorum.

<code lang="perl">
say scalar @words;
say scalar localtime();
</code>

programını çalıştırdığımızda

<code>
3
Mon Nov  7 21:02:41 2011
</code>

çıktısını verecektir.

<h2>Perl'de bir dizinin uzunluğu veya boyu</h2>

Perl'de bir dizinin boyunu, yani elemanlarının sayısını <hl>scalar()</hl> fonksiyonunu kullanarak tekli bağlama zorlamak yoluyla alabilirsiniz.

<h2>Kurnaz yöntem</h2>

Bazen aşağıdaki gibi kod görebilirsiniz:

<code lang="perl">
0 + @words;
</code>

Bu dizinin boyutunu almanın kurnaz yöntemidir. <hl> + </hl> işlem imgesi her iki tarafta da tekli bağlam oluşturur. Yani bu ifadede kullanılan <hl>@words</hl> boyutunu verecektir. Sıfır eklemek bir sayının değerini değiştirmez, bu nedenle yukarıdaki ifade dizinin boyutunu verir.

<hl>scalar</hl> fonksiyonunu kullanarak biraz daha uzun ama daha açık yazmanızı öneririm.

