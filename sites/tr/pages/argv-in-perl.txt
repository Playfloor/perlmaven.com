=title @ARGV in Perl
=timestamp 2013-12-10T08:13:10
=indexes @ARGV, $ARGV[], $0, shift, argc
=status show
=original argv-in-perl
=books beginner
=author szabgab
=translator kbeyazli
=comments_disqus_enable 0

=abstract start

Eğer bir Perl scripti yazdi iseniz, mesela adı <b>programming.pl</b> olsun,
kullanıcılar komut satırından <b>perl programming.pl</b> komutunu kullanarak çalıştırabilirler .

Ayrica şu şekilde komut satırı argümanları gecebilirler. <b>perl programming.pl -a --machine remote /etc</b>.

Kullanıcıların bu şekilde parametre girmesini kimse engelleyemez fakat script bu değeleri önemsemeyecektir.
Bu durumda soru şu, programcı kişi, eğer değer geçilmiş ise bu değerleri nasıl bilecek?

=abstract end

<h2>Komut satırı</h2>

Perl otomatik olarak adı @ARGV olan, komut satırından girilen değerleri tutan bir dizi sağlar.
Bu değişkeni tanımlamak zorunda değilsiniz, hatta <hl>use strict</hl> ifadesi kullanılmışsa bile.

Bu değişken her zaman vardır ve komut satırından girilen değerleri tutar .

Eğer parametre yok ise, dizi boş olacaktır. Komut satırından girilen bir parametre var ise, o değer 
<hl>@ARGV</hl> dizisinin tek elemanı olacaktır. Yukarıdaki örnekte <hl>@ARGV</hl> dizisi aşağıdaki elemanları içerir:
-a, --machine, remote, /etc

Şimdi bunu pratikte görelim:

Aşağıdaki kodu <b>programmimg.pl</b> olarak kaydedin.
 
<code lang="perl">
use strict;
use warnings;
use Data::Dumper qw(Dumper);

print Dumper \@ARGV;
</code>

Şu şekilde çalıştırın: <hl>perl programming.pl -a --machine remote /etc</hl> çıktı şu olacaktır:

<code>
$VAR1 = [
          '-a',
          '--machine',
          'remote',
          '/etc'
        ];
</code>

Göreceğiniz üzere <hl>@ARGV</hl> dizisinin içeriğini basmak için <hl>Data::Dumper</hl>
kütüphanesinin <hl>Dumper</hl> fonksiyonunu kullandık.

Eğer başka bir programlama dilinden geliyor iseniz, aşağıdaki soruyu merak ediyor olabilirsiniz:
<b>Perl programının adı nerede?</b>

<h2>Scriptin adı $0 değişkeni içindedir</h2>

Çalıştırılan programın adı, yukarıdaki örnekte <b>programming.pl</b>, her zaman <hl>$0</hl>
adlı Perl değişkeni içindedir. (Lutfen, <hl>$1</hl>, <hl>$2</hl>, vs. gibi tanımlamaların ilişkili olmadığını not ediniz)

<h2>C programcısı</h2>

Eğer <b>C programlama dili</b> biliyor iseniz, bu size <b>argv</b> ile benzer gelecektir, tek farkı
Perl'deki <hl>@ARGV</hl> dizisi program adını <b>içermez</b>. 
Script adı <hl>$0</hl> değişkeninde tutulur. Ayrıca <b>argc</b> gibi bir değişkene gerek yoktur,
çünkü <a href="/scalar-and-list-context-in-perl"> @ARGV dizisindeki eleman sayısını</a>
<hl>scalar</hl> fonksiyonunu kullanarak veya dizinin 
<a href="/scalar-and-list-context-in-perl">scalar içeriğini</a> alarak kolayca alabilirsiniz.

<h2>Unix/Linux Kabul Programlama</h2>

Eğer <b>Unix/Linux Shell programlama</b> dünyasindan geliyor iseniz, <hl>$0</hl>
değişkeninin burada da script adi olduğu size tanıdık gelecektir. Kabuk programlada ayrıca <hl>$1</hl>, <hl>$2</hl>, vs.
diğer komut satırı parametrelerini tutar. Bu değişkenler Perl dilinin 
düzenli ifadeler (regular expressions) konusunda kullanılır. Komut satırı argumanları <hl>@ARGV</hl> dizisinde tutulur. Benzer
şekilde <hl>$*</hl> ifadesi Unix/linux kabuk programlamada kullanılır

<h2>@ARGV dizisinden komut satırı argümanlarına nasıl erişilir</h2>

<hl>@ARGV</hl> diğer diziler gibi <a href="/perl-arrays">Perl'de bir dizidir</a>.
Programcı tarafından oluşturulan dizilerden tek farkı, tanımlamaya gerek olmayışı ve
program başladığında Perl tarafından dolduruluşudur.

Bu durum dışında, <a href="/perl-arrays">normal dizi</a> gibi ele alınabilir.
<hl>foreach</hl> kullanarak elemanları uzerinde gezebilir, veya indeks kullanarak elemanlara teker teker erişebilirsiniz: <hl>$ARGV[0]</hl>.

Ayrıca dizi üzerinde<a href="/manipulating-perl-arrays">shift, unshift, pop or push</a> komutlarını kullanabilirsiniz.

Aslında, <hl>@ARGV</hl> dizisinin sadece içeriğini görüntülemekle kalmayıp, içeriğini de değiştirebilirsiniz.

Komut satırında tek bir değer bekliyor iseniz bu değerin ne olduğunu kontrol edebilir, ya da <hl>$ARGV[0]</hl>
değerine bakarak geçerli olup olmadığını anlayabilirsiniz. 2 değişken bekliyor iseniz <hl>$ARGV[1]</hl> değerine de bakarsınız.

Örnek olarak bir telefon defteri oluşturalım. Bir isim girildiğinde, uygulama isme karşılık gelen numarayı ekrana basacak.
İsim ve numara girilir ise program bu ikiliyi "veritabanı"na kaydedecek.
(Kodun veritabanı ile ilgili kısmını ele almayacağız, işlem yapılmış varsayacağız.)

Parametrelerin <hl>$ARGV[0]</hl> içinde ve belki ayrıca <hl>$ARGV[1]</hl> içinde tutulacağını, ama
diğer elemanların bir anlam ifade etmediğini biliyoruz.
$ARGV[0] and benzeri değişkenler yerine kendi adlandırdığınız değişkenleri kullanmak her zaman daha iyidir.
Bu nedenle yapmak isteyeceğimiz ilk iş dizi değerlerini oluşturduğumuz değişkenlere kopyalamak:

Aşağıdaki çalışan bir kod:

<code lang="perl">
my $name   = $ARGV[0];
my $number = $ARGV[1];
</code>

Fakat aşağıdaki çok daha hoş:

<code lang="perl">
my ($name, $number) = @ARGV;
</code>

Şimdi tüm örneği görelim (veritabanı kısmı hariç).
Aşağıdaki kodu <b>programming.pl</b> olarak kaydedin.

<code lang="perl">
use strict;
use warnings;

my ($name, $number) = @ARGV;

if (not defined $name) {
  die "Need name\n";
}

if (defined $number) {
  print "Save '$name' and '$number'\n";
  # save name/number in database
  exit;
}

print "Fetch '$name'\n";
# look up the name in the database and print it out
</code>

Değerleri <hl>@ARGV</hl> dizisinden kopyaladıktan sonra, isim girilip girilmediğini kontrol ediyoruz.
Eğer girilmemiş ise, ekrana bir hata mesajı basacak ve scriptten çıkacak olan <hl>die</hl> komutunu çağırıyoruz.

Eğer isim var ise, numarayı kontrol ediyoruz. Eğer numara var ise 
veritabanına kaydediyor (yukarıda ele alınmadı) ve scriptten çıkıyoruz.

Numara yok ise veritabanindan isim bilgisini çekmeye çalışıyoruz. (Bu kısım da yukarıda ele alınmadı.)

Şimdi nasıl çalıştığına bakalım: ($ işareti komut satırından gelmektedir, biz bunu yazmayacağız.)

<code>
$ perl programming.pl Foo 123
Save 'Foo' and '123'

$ perl programming.pl Bar 456
Save 'Bar' and '456'

$ perl programming.pl John Doe 789
Save 'John' and 'Doe'
</code>

Ilk iki çağrı OK ama sonuncusu iyi gözükmüyor.
"John Doe"'nun telefon numarasını 789 olarak kaydetmek istedik, ama scriptimiz bunun yerine
"John"'un telefon numarasını "Doe" olarak kaydetti.

Sebep basit, ve Perl ile yapılacak bir şey bulunmamakta. Bu çağrı diğer herhangi bir dilde de bu şekilde çalışacaktı.
Scripti çalıştırdığınız kabuk veya komut satırı, çalıştırma komutunu ayırır ve değerleri <hl>@ARGV</hl> dizisine koyması için
Perl'e geçirir. Hem Unix/Linux kabuk ve Windows Komut Satırı program calistirma kodundan sonraki ifadeleri boşluğu baz alarak
ele alır. Bu nedenle <hl>perl programming.pl John Doe 789</hl> şeklinde çağrı yaptığımızdan, 3 parametre scriptimize geçirildi. 
Doğu çalışmasını sağlamak için çift tırmak içinde boşluk kullanılabilir:

<code>
$ perl a.pl "John Doe" 789
Save 'John Doe' and '789'
</code>

Siz, programcı olarak bundan daha fazlasını yapamazsınız.

<h2>Argümanları kontrol etmek</h2>

Bazen girilen arguman sayısının beklediğiniz sayıyı aşıp aşmadığını kontrol edebilirsiniz.
Bu kullanıcının yukarıdaki hatayı yapmasını engeller, ama ya kullanıcı John Doe'nun 
telefon numarasını almak ister ve çift tırnakları unutursa:

<code>
perl a.pl John Doe
Save 'John' and 'Doe'
</code>

Bu durumda parametre sayısı dogru rakam olan 2'dir.

Bu durumda da, küçük bir geliştirme yapıp <hl>$number</hl> değişkeninin içeriğinin telefon numarası
olarak kabul edilebilecek formatta olup olmadığını kontrol edebilirsiniz. Bu hata olasılığını azaltacaktır.
Fakat hala kusursuz ve evrensel bir çözüm olmayacaktır:
Başka uygulamalarda birçok sayıda aynı kısıtlarda parametreler olabilir.

Maalesef <hl>@ARGV</hl> dizisini "manual" ayrıştırırken yapabileceğimiz fazla şey yok.
Bir başka makalede <hl>Getopt::Long</hl> ve hayatımızı daha kolaylaştıran benzer 
kütüphanelere değineceğiz, şimdi bir başka basit durumu görelim.


<h2>tek bir parametreyi shift etmek</h2>

Kullanıcının komut satırından bir dosya adı gireceğini varsayalım
Bu durumda aşağıdaki kodu yazarsınız:

<code lang="perl">
my $filename = shift or die "Usage: $0 FILENAME\n";
</code>

Daha kolay açıklayabilmek için kodu 2 bölüme ayıralım:

<hl>my $filename = shift</hl>

Normal şartlarda <a href="/manipulating-perl-arrays">shift</a> parametre olarak bir dizi alır
ama bu ornekte parametresiz kullandık. Bu gibi durumlarda shift komutu
<hl>@ARGV</hl> dizisi üzerinde çalışır. Bu nedenle yukarıdaki kod <hl>@ARGV</hl> dizisinin birinci değerini
<hl>$filename</hl> değişkenine atar. (En azından kod bir subroutine içinde değilse.)

Bu durumda aşağıdaki koda sahip oluruz:
<hl>$filename or die "Usage: $0 FILENAME\n"</hl>

Bu <a href="/boolean-values-in-perl">boolean</a> bir ifadedir.
Eğer <hl>$filename</hl> değişkeni dosya adını içeriyor ise
<a href="/boolean-values-in-perl">True olarak düşünülecek</a> ve script
<hl>or die ...</hl> bölümünü çalıştırmadan çalışmaya devam edecektir.
Diğer taraftan, eğer @ARGV dizisi boş ise, <hl>$filename</hl> <hl>undef</hl> olarak tanımlanacak, 
<a href="/boolean-values-in-perl">False yorumlanacak</a>
ve Perl sağ tarafta yer alan <hl>or</hl> durumunu çalıştıracağından,
ekrana bir mesaj basıp, scriptten çıkacaktır. 

Etkileyici biçimde, bu kod parçası komut satırından bir değer girilip girilmediğini kontrol eder. Değer
<hl>$filename</hl> değişkenine kopyalanır. Değer yok ise, script <hl>die</hl> komutu ile sonlanır.

<h2>Küçük bir bug</h2>

Yukarıdaki kodda küçük bir bug bulunmakta. Kullanıcı dosya adı olarak 0 değerini girerse. Yine False olarak
işlem görecek ve script bu dosyaya işlem yapmayı reddedecektir. Soru şu: Bu çok önemli mi?
Kodumuzun dosya adı <b>0</b> olan bir dosyaya işlem yapmaması ihtimalini göze alabilir miyiz... ?

<h2>Kompleks durumlar</h2>

Yukarıdaki bir veya iki parametreli durumlardan cok daha kompleks başka durumlar da mevcuttur.
Bu gibi durumlar için büyük ihtimalle <hl>Getopt::Long</hl> gibi ne tür parametreleri kabul edeceğinize 
bağlı olarak <hl>@ARGV</hl> dizisinin içeriğini analiz edebilecek araçlar kullanmak isteyeceksiniz.


