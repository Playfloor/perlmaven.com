=title Не используйте интерполяцию в printf (отсутствующий аргумент в sprintf ...)
=timestamp 2014-08-18T21:26:01
=indexes printf, sprintf
=status show
=original dont-interpolate-in-printf
=author szabgab
=translator ymyasoedov
=comments_disqus_enable 0

=abstract start

Однажды, когда я запустил один из моих скриптов, я получил исключение <hl>Missing argument in sprintf at ...</hl> (отсутствующий аргумент в sprintf ...).

В общем я написал небольшой пример, демонстрирующий проблему. Взглянем на пример:

=abstract end

В корне проекта у меня находятся два файла, скрипт и модуль, который представляет текущий обрабатываемый файл:

<code>
dir/
   printf_interpolate.pl
   lib/App/File.pm
</code>


Скрипт <hl>printf_interpolate.pl</hl> содержит следующий код:

<code lang="perl">
use strict;
use warnings FATAL => 'all';

use App::File;

my $LIMIT = 80;

my $file = read_params(@ARGV);
_log("START");
check_file($file);
_log("DONE");


sub read_params {
    my $filename = shift or die "Usage: $0 FILENAME\n";
    my $file = App::File->new($filename);
    return $file;
}

sub check_file {
    my ($file) = @_;

    open my $fh, '<', $file->filename or die;
    while (my $line = <$fh>) {
        my $actual = length $line;
        if ($actual > $LIMIT) {
            _log(sprintf "Line is too long ($actual > $LIMIT) ($line) (file %s)", $file->filename);
        }
    }
}


sub _log {
    my $str = shift;
    say $str;
}
</code>

Всё, что делает код в этом примере, это построчное чтение файла с проверкой длины строк. Если строка слишком длинная, вызывается функция <hl>_log</hl> и выводится предупреждение.

Модуль <hl>/lib/App/File.pm</hl> содержит следующий код:

<code lang="perl">
package App::File;
use strict;
use warnings;

sub new {
    my ($class, $filename) = @_;
    return bless {filename => $filename}, $class;
}
sub filename {
    my ($self) = @_;
    return $self->{filename};
}

1;
</code>

Это простой модуль, который представляет файл. В реальном коде реализация класса будет значительно сложнее, но в нашем случае такого примитивного класса достаточно.

Итак, что произойдёт, если я запущу скрипт, передав этот скрипт в качестве параметра? То есть, что произойдёт, если скрипт не будет содержать ни одной строки, длиннее 80 символво?

<code>
cd dir/
perl printf_interpolate.pl printf_interpolate.pl
</code>

После вывода слова "START" мы получим следующее исключение:
<hl>Missing argument in sprintf at printf_interpolate.pl line 27, <$fh> line 27.</hl>
pointing us to the line where we call <hl>sprintf</hl>.

На самом деле это <b>предупреждение</b>, т.к. код <hl>use warnings FATAL => 'all'</hl> включает обработку всех предупреждений как фатальные исключения.

Проблема заключается в том, что некоторые строки содержат фрагменты, которые похожи на заполнители, использующиеся в функции sprintf.
Т.к. <hl>sprintf</hl> содержит некоторые встроенные (интерполированные) переменные (в частности, переменную <hl>$line</hl>), после интерполяции строка будет содержать более одного заполнителя <hl>%s</hl>.

Если заменить проблемный код на следующий:
<code lang="perl">
  _log(sprintf "Line is too long ($actual > $LIMIT) (%s) (file %s)", $line, $file->filename);
</code>

то это решит указанную выше проблему, т.к. мы вставили ещё один форматтер <hl>%s</hl>, который будет раскрыт во время вызова <hl>sprintf</hl>.

Если запустить на выполнение код, то теперь мы увидим следующее:

<code>
START
Line is too long (104 > 80) (            _log(sprintf "Line is too long ($actual > $LIMIT) (%s) (file %s)", $line, $file->filename);
) (file printf_interpolate.pl)
DONE
</code>

Ну вот, теперь лучше.

Конечно, если мы вынесли одну переменную из строки, то мы должны поступить таким же образом с двумя оставишимися переменными:

<code lang="perl">
  _log(sprintf "Line is too long (%s > %s) (%s) (file %s)", $actual, $LIMIT, $line, $file->filename);
</code>

<h2>Короткий пример</h2>

Следующий фрагмент кода может воспроизвести предупреждение:

<code lang="perl">
use strict;
use warnings;

my $name = 'Foo';
#my $smalltalk = 'how are you?';
my $smalltalk = '%s hi?';

printf "Hello %s, $smalltalk\n", $name; 
</code>

<code>
Missing argument in printf at printf_interpolate_short line 8.
Hello Foo,  hi?
</code>

<h2>Итог</h2>

Не встраивайте переменные в функции <hl>printf</hl> и <hl>sprintf</hl>, т.к. переменные могут содержать специальные символы, которые могут управлять поведением этих функций.
